//===== rAthena Script =======================================
//= Rock Scissors Roulette
//===== By: ==================================================
//= acky
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Plays a hybrid Russian Roulette Rock Scissors Paper game.
//===== Additional Comments: =================================
//= Prizes customizable, Added emotions.
//= 1.2 Fixes by Blackthunder and me [Poki#3]
//============================================================

quiz_02,346,368,2	script	Jokenpô	85,{
	set .@counter,1;
	mes "Ei você! Quer jogar um jokenpô explosivo?";
	next;
	menu "Quero jogar.",PLAY,"Explique as regras.",RULES,"Sair",LEAVE;
SAME:
	mes "Empatou! De novo!";
	next;
PLAY:
	mes "Pedra... Papel...";
	set .@opp, rand(1,3);
	menu "^0000FFPEDRA!",-,"^FF0000TESOURA!",TESOURA,"^00FF00PAPEL!^000000",PAPEL;

  	if (.@lastchoice == 1) set .@opp, rand(1,3);
  	if (.@opp == 1) emotion e_rock;
  	else if (.@opp == 2) emotion e_scissors;
  	else emotion e_paper;
  	set .@lastchoice,1;
	if (.@opp == 1) goto SAME;
	if (.@opp == 2) goto WIN;
	goto LOSE;

TESOURA:
	if (.@lastchoice == 2) set .@opp,rand(1,2);
  	if (.@opp == 1) emotion e_rock;
  	else if (.@opp == 2) emotion e_scissors;
  	else emotion e_paper;
  	set .@lastchoice,2;
	if (.@opp == 1) goto LOSE;
	if (.@opp == 2) goto SAME;
	goto WIN;

PAPEL:
	if (.@lastchoice == 3) set .@opp,rand(2,3);
  	if (.@opp == 1) emotion e_rock;
  	else if (.@opp == 2) emotion e_scissors;
  	else emotion e_paper;
  	set .@lastchoice,3;
	if (.@opp == 1) goto WIN;
	if (.@opp == 2) goto LOSE;
	goto SAME;

WIN:
	mes "Droga, você ganhou!";
	emotion e_swt2;
	next;
	set .@win, 1;
	goto YOUPULL;

LOSE:
	emotion e_heh;
	mes "Boorah! You Lose!";
	next;
	set .@win, 0;

YOUPULL:
	mes .@counter +" of 6";
	if ( .@counter == 6 )
		mes "Hora de rezar";
	set .@pull, rand( 1,( 7 -.@counter ) );
	set .@counter, .@counter +1;
	next;
	if ( .@pull == 1 ) {
		if ( .@win ) goto KILL;
		specialeffect2 EF_SUI_EXPLOSION;
		emotion e_gg;
		percentheal -100,-100;
		mes "*^0000FFClick^000000* *^FF0000BANG^000000*";
		mes "Você está morto!";
		close;
	}
	emotion e_pif;
	mes "*^0000FFClick^000000* whew...";
	goto PLAY;

RULES:
	mes "As regras são as seguintes:";
	mes "Eu tenho comigo ^FF00001^000000  pistola ^FF00001^000000 com uma bala. Primeiro nós jogamos ^0000FFPedra^000000 ^00FF00Papel ^FF0000Tesoura^000000 . O perdedor puxa o gatilho. O vencedor,é aquele que terminar vivo, Total de 6 rounds.";
	mes "Me vença para ganhar um prêmio.";
	next;
	menu "Quero jogar.",-,"Não obrigado.",LEAVE;
	mes "Vamos começar então...";
	next;
	goto PLAY;

KILL:
	specialeffect EF_SUI_EXPLOSION;
	emotion e_omg;
	mes "*^0000FFClick^000000* *^FF0000BANG^000000*";
	mes "OWWW @#$%^!! ESSA DOEU PRA CARALHO!!";
	next;
	mes "Congratulations! You have won...";
	switch( rand( 1,10 ) ) {
		case 1: setarray .@reward[0], 10,984; break;
		case 3: setarray .@reward[0],100,601; break;	// 100x Fly Wings
		case 4: setarray .@reward[0],  8,603; break;	// 8x Old Blue Box
		case 5: setarray .@reward[0],  4,617; break;	// 4x Old Violet Box
		case 6: setarray .@reward[0],  1,616; break;	// 1x Old Card Album
		case 7: setarray .@reward[0], 10,604; break;	// 10x Dead Branch
		case 8: setarray .@reward[0],  3,969; break;	// 3x Gold
		case 10: setarray .@reward[0],20,505; break;	// 20x Blue Potion
		case 2:
		case 9:
			setarray .@reward[0], 10,985;
	}
	mes .@reward[0] +"x "+ getitemname( .@reward[1] ) +"!";
	getitem .@reward[1], .@reward[0];
	close;

LEAVE:
	mes "Pansy.";
	close;
}
